use Libraries.Game.Game
use Libraries.Game.Graphics.Drawable
use Libraries.Game.InputMonitor
use Libraries.Interface.Events.KeyboardEvent
use Libraries.Game.Graphics.Color
use Libraries.Compute.Random
use Libraries.Interface.Events.CollisionListener2D
use Libraries.Interface.Events.CollisionEvent2D
use Libraries.Compute.Math
use Libraries.Interface.Events.KeyboardListener
use Libraries.Sound.Audio
use Libraries.Containers.Array
use Libraries.Containers.Iterator

class Main is Game, CollisionListener2D, KeyboardListener
    Drawable hero
    Drawable target
    Array<Drawable> ground
    Color c
    InputMonitor monitor
    KeyboardEvent keys
    Random r

    integer speed = 200
    integer targetSpeed = 100
    integer xSpeed = 100
    integer ySpeed = 100
    integer score = 0
    integer heroRadius = 10
    integer targetSize = 30
    integer jumpSpeed = 150
    integer groundHeight = 100
    integer groundWidthMin = 100
    integer groundWidthMax = 400
    integer groundSpeed = 50
    integer pitWidth = 60

    number time = 0
    number cumTime = 0
    number jumpTime = 0.75
    number cumJumpTime = 0
    
    boolean inJump = false

    Audio audio

    action Main
        StartGame()
    end

    action CreateGame
        hero:LoadFilledCircle(heroRadius)
        hero:SetPosition(0, 100)
        target:LoadFilledRectangle(targetSize, targetSize, c:Red())
        target:SetPosition(400, 300)
        hero:SetCollidable(true)
        target:SetCollidable(true)
        Add(hero)
        Add(target)
        time = r:RandomNumber()
        AddCollisionListener(me)
        AddKeyboardListener(me)

        number cumFloor = 0
        repeat 10 times
            Drawable newFloorPiece
            newFloorPiece:LoadFilledRectangle(r:RandomIntegerBetween(groundWidthMin, groundWidthMax), 100, c:Green())
            newFloorPiece:SetCollidable(true)
            newFloorPiece:SetPosition(cumFloor, 0)            
            cumFloor = cumFloor + newFloorPiece:GetWidth() + pitWidth
            Add(newFloorPiece)
            ground:AddToEnd(newFloorPiece)
        end

        //A little adorable royalty free music piece available 
        //here: http://www.bensound.com/royalty-free-music/track/cute
        audio:Load("media/bensound-cute.ogg")
        audio:EnableLooping()
        audio:Play()
    end

    action BeginCollision(CollisionEvent2D e)
        inJump = false
    end

    action PressedKey(KeyboardEvent event)
        if event:keyCode = event:SPACE
            if inJump = false
                inJump = true
                cumJumpTime = 0
                if hero:GetX() < groundHeight
                    output "you die"
                end
            end
        end
    end

    action Update(number seconds)
        cumTime = cumTime + seconds
        if cumTime > time
            cumTime = 0
            time = r:RandomNumber()
            xSpeed = r:RandomIntegerBetween(-targetSpeed, targetSpeed)
            ySpeed = r:RandomIntegerBetween(-targetSpeed, targetSpeed)
        end
        number newX = target:GetX() + seconds * xSpeed
        number newY = target:GetY() + seconds * ySpeed
        if newX < targetSize or newX > 800 - targetSize
            xSpeed = -xSpeed
        end
        if newY < targetSize or newY > 600 - targetSize
            ySpeed = -ySpeed
        end
        target:SetX(newX)
        target:SetY(newY)
        number x = hero:GetX()
        if monitor:IsKeyPressed(keys:RIGHT)
            x = x + seconds * speed
            if x > (800 - heroRadius * 2)
                x = 800 - heroRadius * 2
            end
        elseif monitor:IsKeyPressed(keys:LEFT)
            x = x - seconds * speed
            if x < 0
                x = 0
            end
        elseif monitor:IsKeyPressed(keys:EQUALS)
            groundSpeed = groundSpeed + 20
            if targetSpeed > 0
                targetSpeed = targetSpeed + 20
            else
                targetSpeed = targetSpeed - 20
            end
        elseif monitor:IsKeyPressed(keys:MINUS)
            groundSpeed = groundSpeed - 20
            if targetSpeed = 0
                targetSpeed = 0
            elseif targetSpeed > 0
                targetSpeed = targetSpeed - 20
            else
                targetSpeed = targetSpeed + 20
            end
        end
        hero:SetX(x)

        if inJump
            cumJumpTime = cumJumpTime + seconds
            if cumJumpTime < jumpTime * 0.40
                hero:SetY(hero:GetY() + seconds * jumpSpeed)
            elseif cumJumpTime < jumpTime * 0.60
            //pause at the top of the jump
            else
                hero:SetY(hero:GetY() - seconds * jumpSpeed)
            end
        end
        Iterator<Drawable> it = ground:GetIterator()
        repeat while it:HasNext()
            Drawable floorPiece = it:Next()
            number itemX = floorPiece:GetX()
            if itemX + floorPiece:GetWidth() < 0
                ground:RemoveFromFront()
            else
                itemX = itemX - seconds * groundSpeed
                floorPiece:SetX(itemX)
            end
        end
    end
end
